plugins {
    id 'java'
    id 'application'
}
mainClassName = 'edu.rochester.cif.cerberus.Cerberus'

group 'edu.rochester.cif.cerberus'
version '1.0'

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

processResources {
    inputs.property 'version', project.version

    from (sourceSets.main.resources.srcDirs) {
        include 'app.properties'

        expand 'VERSION': project.version
    }

    from (sourceSets.main.resources.srcDirs) {
        exclude 'app.properties'
    }
}

jar {
    manifest {
        attributes 'Main-Class': "$mainClassName"
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    //config parsing
    implementation 'commons-cli:commons-cli:1.4'

    //using old version of jserialcomm to be compatible with Ben's code
    implementation 'com.fazecast:jSerialComm:1.3.11'

    //better logging that I don't intend to write
    implementation 'org.apache.logging.log4j:log4j-api:2.12.1'
    implementation 'org.apache.logging.log4j:log4j-core:2.12.1'
}
